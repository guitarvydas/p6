unq {
 Program = Sexpr+
 Sexpr = List | Atom

  Atom =
    | "#f" ~#id -- false
    | "#t" ~#id -- true
    | #integer -- integer
    | #string -- string
    | #symbol -- symbol

  integer = digit+
  string = "\"" (~"\"" any)* "\""
  symbol = id ("." id)*
  id = firstLetter followLetter*
  firstLetter = letter | (~separator ~unprintable any)
  followLetter = letter | digit | (~separator ~unprintable any)
  special  = ";" | "#" | "(" | ")" | "'" | "`" | "," | "."
  unprintable = ("\x00" .. "\x20") | "\x7F"
  separator = space | special | end

  List =
    | "(" ")" -- nil
    | "'" Sexpr -- quoted
    | "`" Sexpr -- backquoted
    | "," Sexpr -- commaexpanded
    | "(" Sexpr+ "." Sexpr ")" -- dotted
    | "(" Sexpr+ ")" -- list

  comment = ";" (~"\n" any)* "\n"
  space += comment
}

//+ a
//+ b.c
//+ ()
//+ (d)
//+ (e f)
//+ 'g
//+ '(h i)
//+ `j
//+ `(k l)
//+ `(m ,n o)
//+ ,p
//+ (q r s t . u)
